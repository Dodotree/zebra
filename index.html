<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <meta http-equiv="X-UA-Compatible" content="ie=edge" />
        <title>Zebra</title>
        <link rel="stylesheet" href="./index.css" />
    </head>

    <body>
        <header>
            <h1>Zebra WebGL shader and WebCam Tuner</h1>
        </header>

        <main>


    <!-- Vertex shader program -->
    <script id="shader-vs" type="x-shader/x-vertex">
        #version 300 es
        in vec4 a_position;
        void main() {
            gl_Position = a_position - 0.5;
        }
    </script>

    <!-- Fragment shader program -->
    <script id="packing-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D u_texture;

        layout(location = 0) out vec4 outColor;
        layout(location = 1) out vec4 fragCoord;

        float threshold = 0.39; // threshold for black pixels, normalized to [0, 1] range
                                // 0.39 is 100, 0.24 is 60
                                // advised to err on higher side
        int iwidth = 8; // width of the pixel block, packed into 8 bits
        int iheight = 4; // height of the pixel block, packed into RGBA channels
        vec4 bytes = vec4(0.0, 0.0, 0.0, 0.0);

        void main() {

            // gl_FragCoord gives exact pixel coordinates in window space, not interpolated
            int x = int(gl_FragCoord.x) * 8;
            int y = int(gl_FragCoord.y) * 4;
            ivec2 xy = ivec2(x, y);
            for (int j = 0; j < iheight; j++) {
                uint byteValue = 0u;
                for(int i = 0; i < iwidth; i++) {
                    float normCol = texelFetch(u_texture, xy + ivec2(i,j), 0).r;
                    if (normCol < threshold) { // normalized
                        byteValue =  byteValue | (1u << uint(i));
                    }
                }
                bytes[j] = float(byteValue) / 255.0; // normalize to [0, 1] range
            }
            outColor = bytes;
            fragCoord = gl_FragCoord;
        }
    </script>

    <!-- Fragment shader program -->
    <script id="dilation-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D p_texture;
        out vec4 outColor;

        int iwidth = 8; // width of the pixel block, packed into 8 bits
        int iheight = 4; // height of the pixel block, packed into RGBA channels

        vec4 bytes = vec4(0.0, 0.0, 0.0, 0.0);


        uint[12] getDataBock(sampler2D tex) {
            // In GLSL, arrays are automatically initialized to zero if no initializer is provided,
            // though this behavior can vary by GLSL version and implementation.
            // For most reliable results, explicitly initialize each element.
            uint[12] data = uint[12](0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u);
            ivec2 xy = ivec2(gl_FragCoord.xy);

            for (int j = -1; j < 2; j++) {
                for(int i = -1; i < 2; i++) {
                    vec4 bytes = texelFetch(tex, xy + ivec2(i, j), 0) * 255.0; // .rgba
                    for(int cj = 0; cj < 4; cj++) {
                        data[4*j + 4 + cj] = data[4*j + 4 + cj] | (uint(bytes[cj]) << uint(8 * i + 8));
                    }
                }
            }
            return data;
        }

        uint[14] getTransposedBock(uint[12] data) {
            uint[14] cols = uint[14](0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u);
            for (int row = 0; row < 12; row++) {
                // Extract the middle 12 bits (bits 6-17) from the 24-bit row
                uint middleBits = (data[row] >> 5u) & 0x3FFFu;
                for (int col = 0; col < 14; col++) {
                    uint bitValue = (middleBits >> uint(13 - col)) & 1u;
                    cols[13-col] = (cols[13-col] & ~(1u << uint(row))) | (bitValue << uint(row));
                }
            }
            return cols;
        }

        void main() {

            uint data[12] = getDataBock(p_texture);
            uint cols[14] = getTransposedBock(data);

            for (int j = 0; j < iheight; j++) {
                uint byteValue = 0u;
                for(int i = 0; i < iwidth; i++) {
                    uint crossRow = (data[j + 4] >> uint(i+5)) & 0x7Fu;
                    uint crossCol = (cols[i + 3] >> uint(j+1)) & 0x7Fu;
                    // if( bool(data[j + 4] & (1u << uint(i+8))) )  {
                    if( bool(crossRow + crossCol) ) {
                        // If any of the bits in the cross is set, set the bit in the byte
                        byteValue =  byteValue | (1u << uint(i));
                    }
                }
                bytes[j] = float(byteValue) / 255.0;
            }
            outColor = bytes;
        }
    </script>

    <!-- Fragment shader program -->
    <script id="mask-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D p_texture;
        out vec4 outColor;

        int iwidth = 8;   // tile 8x16 for convenience instead of 10x15
        int iheight = 16;

        uint countBits8(uint byte) {
            uint val = (byte & 0x55u) + ((byte >> 1u) & 0x55u);
            val = (val & 0x33u) + ((val >> 2u) & 0x33u);
            val = (val + (val >> 4u)) & 0x0Fu;
            return val;
        }

		void main() {
		    int count = 0;
            float sum = 0.0;
            uint[16] data = uint[16](0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u,
                                     0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u);
            ivec2 xy = ivec2(gl_FragCoord.xy);

            for(int j = 0; j < 4; j++) {
                vec4 bytes = texelFetch(p_texture, ivec2(xy.x, xy.y * 4) + ivec2(0, j), 0); // .rgba
                for(int cj = 0; cj < 4; cj++) {
                    data[4*j + cj] = uint(bytes[cj]  * 255.0);
                    count += 8 - int(countBits8( data[4*j + cj] )); // count 0 for background
                }
            }
            outColor = vec4(float(count) / 255.0, 0.0, 0.0, 1.0);
        }

    </script>


    <!-- Fragment shader program -->
    <script id="gray-masked-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D m_texture;
        uniform sampler2D src_texture;
        out vec4 outColor;

        int iwidth = 8;   // tile 8x16 for convenience instead of 10x15
        int iheight = 16;
        float mincount = 43.0; // float for convenience, int. 43 out of 128 tile pixels
        // In Leptonica 50 out of 150 tile pixels, same 1/3 threshold

        vec4 encodeFloat(float v) {
            uint u = floatBitsToUint(v);
            vec4 bytes;
            bytes.r = float(u & 0xFFu) / 255.0;
            bytes.g = float((u >> 8u) & 0xFFu) / 255.0;
            bytes.b = float((u >> 16u) & 0xFFu) / 255.0;
            bytes.a = float((u >> 24u) & 0xFFu) / 255.0;
            return bytes;
        }

        void main() {

            ivec2 xyOrig = ivec2(gl_FragCoord.xy);
            int x = xyOrig.x / iwidth;
            int y = xyOrig.y / iheight;
            float count = 255.0 * texelFetch(m_texture, ivec2(x,y), 0).r;
            if( count < mincount) {
                outColor = vec4(0.0, 0.0, 0.0, 0.0);
                return;
            }

            vec4 accColor = vec4(0.0, 0.0, 0.0, 0.0);
            for(int j = 0; j < iheight; j++) {
                accColor += texelFetch(src_texture, ivec2(xyOrig.x, xyOrig.y + j), 0);
            }

            float gray = dot(accColor.rgb / count, vec3(0.299, 0.587, 0.114));
            // with division by 128 there could be precision issues, so we will use 4 bytes
            outColor = encodeFloat(gray);

            // vec3 redTint = vec3(1.0, 0.3, 0.3);
            // outColor = vec4( mix(vec3(gray), redTint * gray, mask), 1.0);
        }
    </script>


    <!-- Fragment shader program -->
    <script id="gray-down-fs" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;

        uniform sampler2D g_texture;
        out vec4 outColor;

        int iwidth = 8;
        int iheight = 16;


        vec4 encodeFloat(float v) {
            uint u = floatBitsToUint(v);
            vec4 bytes;
            bytes.r = float(u & 0xFFu) / 255.0;
            bytes.g = float((u >> 8u) & 0xFFu) / 255.0;
            bytes.b = float((u >> 16u) & 0xFFu) / 255.0;
            bytes.a = float((u >> 24u) & 0xFFu) / 255.0;
            return bytes;
        }

        float decodeFloat(vec4 rgba) {
            uint r = uint(rgba.r * 255.0);
            uint g = uint(rgba.g * 255.0);
            uint b = uint(rgba.b * 255.0);
            uint a = uint(rgba.a * 255.0);
            // Combine the components
            uint u = r | (g << 8u) | (b << 16u) | (a << 24u);
            return uintBitsToFloat(u);
        }

        void main() {
            int x = iwidth * int(gl_FragCoord.x);
            int y = iheight * int(gl_FragCoord.y);
            float averagedGray = 0.0;

            for(int i=0; i < iwidth; i++) {
                vec4 encodedGray = texelFetch(g_texture, ivec2(x + i, y), 0);
                averagedGray += decodeFloat(encodedGray);
            }
            outColor = encodeFloat(averagedGray);
            // outColor = vec4(averagedGray, averagedGray, averagedGray, 1.0);
        }
    </script>



    <!-- Fragment shader program -->
    <script id="box-blur-y-fs" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;

        uniform sampler2D by_texture;
        out vec4 outColor;

        int radius = 2;


        vec4 encodeFloat(float v) {
            uint u = floatBitsToUint(v);
            vec4 bytes;
            bytes.r = float(u & 0xFFu) / 255.0;
            bytes.g = float((u >> 8u) & 0xFFu) / 255.0;
            bytes.b = float((u >> 16u) & 0xFFu) / 255.0;
            bytes.a = float((u >> 24u) & 0xFFu) / 255.0;
            return bytes;
        }

        float decodeFloat(vec4 rgba) {
            uint r = uint(rgba.r * 255.0);
            uint g = uint(rgba.g * 255.0);
            uint b = uint(rgba.b * 255.0);
            uint a = uint(rgba.a * 255.0);
            // Combine the components
            uint u = r | (g << 8u) | (b << 16u) | (a << 24u);
            return uintBitsToFloat(u);
        }

        void main() {
            ivec2 xy = ivec2(gl_FragCoord.xy);
            float averagedGray = 0.0;
            float fraction = 1.0 / float((radius * 2 + 1) * (radius * 2 + 1));

            for(int j = -radius; j < radius + 1; j++) {
                vec4 encodedGray = texelFetch(by_texture, ivec2(xy.x, xy.y+j), 0);
                averagedGray += decodeFloat(encodedGray);
            }
            averagedGray *= fraction;
            outColor = encodeFloat(averagedGray);
            // outColor = texelFetch(by_texture, xy, 0);
            // outColor = vec4(averagedGray, averagedGray, averagedGray, 1.0);
        }
    </script>


    <!-- Fragment shader program -->
    <script id="box-blur-x-fs" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;

        uniform sampler2D bx_texture;
        out vec4 outColor;

        int radius = 2;

        float decodeFloat(vec4 rgba) {
            uint r = uint(rgba.r * 255.0);
            uint g = uint(rgba.g * 255.0);
            uint b = uint(rgba.b * 255.0);
            uint a = uint(rgba.a * 255.0);
            // Combine the components
            uint u = r | (g << 8u) | (b << 16u) | (a << 24u);
            return uintBitsToFloat(u);
        }

        void main() {
            ivec2 xy = ivec2(gl_FragCoord.xy);
            float averagedGray = 0.0;

            for(int i = -radius; i < radius + 1; i++) {
                vec4 encodedGray = texelFetch(bx_texture, ivec2(xy.x+i, xy.y), 0);
                averagedGray += decodeFloat(encodedGray);
            }
            // no point of encoding if used with interpolated value fetch
            // outColor = vec4(invertedGray, invertedGray, invertedGray, 1.0);
            outColor = vec4(averagedGray, averagedGray, averagedGray, 1.0);
        }
    </script>


    <!-- Fragment shader program -->
    <script id="upscale-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D d_texture;
        uniform sampler2D src_texture;
        out vec4 outColor;

        float iwidth = 8.0;
        float iheight = 16.0;

        void main() {
            ivec2 size = textureSize(d_texture, 0);
            vec2 xyOrig = gl_FragCoord.xy;
            float x = xyOrig.x / iwidth;
            float y = xyOrig.y / iheight;

            vec4 color = texelFetch(src_texture, ivec2(xyOrig), 0);
            float gray = dot(color.rgb, vec3(0.299, 0.587, 0.114));

            // vec4 color = texelFetch(d_texture, ivec2(x,y), 0);

            // interpolated texture fetch for smoother scaling
            vec4 mask = texture(d_texture, vec2(x / float(size.x), y / float(size.y)));

            gray =  gray / mask.r;
            gray = (gray < 0.625) ? 0.0 : 1.0; //   0.625 is 160 / 255

            outColor  = vec4(gray, gray, gray, 1.0);
            // outColor = mask;
        }
    </script>


    <!-- Fragment shader program -->
    <script id="unpacking-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D p_texture;
        out vec4 outColor;

        int iwidth = 8; // width of the pixel block, packed into 8 bits
        int iheight = 4; // height of the pixel block, packed into RGBA channels

        void main() {

            ivec2 xyOrig = ivec2(gl_FragCoord.xy - 0.5);
            int dx = xyOrig.x % iwidth;
            int dy = xyOrig.y % iheight; // to decide index in .rgba array
            int x = (xyOrig.x - dx) / iwidth;
            int y = (xyOrig.y - dy) / iheight;

            uint byte = uint( texelFetch(p_texture, ivec2(x,y), 0)[dy] * 255.0 );
            uint bit = byte & (1u << uint(dx));
            float val = float(bool(bit));
            outColor = vec4(val, val, val, 1.0);
        }
    </script>


    <!-- Fragment shader program -->
    <script id="comparison-fs" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;

        uniform sampler2D src_texture;
        uniform sampler2D p_texture;
        uniform float u_threshold;

        out vec4 outColor;

        void main() {
            ivec2 texCoord = ivec2(gl_FragCoord.xy - 0.5);
            vec4 color1 = texelFetch(src_texture, texCoord, 0);
            vec4 color2 = texelFetch(p_texture, texCoord, 0);

            if(all(lessThan(abs(color1 - color2), vec4(u_threshold)))) {
                discard;
            }

            // Otherwise output difference, or something, doesn't matter
            outColor = abs(color1 - color2);
        }
    </script>


    <!-- Fragment shader program -->
    <script id="copy-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D up_texture;
        out vec4 outColor;

        void main() {
            ivec2 texCoord = ivec2(gl_FragCoord.xy - 0.5);
            outColor = texelFetch(up_texture, texCoord, 0);
        }
    </script>




    <script type="module">
        const main = document.querySelector("main");
        main.appendChild(
            document.createElement("screen-logger"),
        );
        document.body.insertBefore(
            document.createElement("media-menu"),
            main
        );
    </script>

    <script type="module" src="./utils/Logger.js"></script>
    <script type="module" src="./mediaMenu.js"></script>


        <aside class="warning" role="alert">
        ⚠️ <strong>Under Construction:</strong> WebgGL shaders are still being developed and tested.
        If you just want to work with the video click on the "video" checkbox above WebGL window.
        Some features may not work as expected, and performance may vary across different devices.
        Please report any issues you encounter.
        </aside>

        </main>

    </body>
</html>
