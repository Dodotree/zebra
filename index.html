<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <meta http-equiv="X-UA-Compatible" content="ie=edge" />
        <title>Zebra</title>
        <link rel="stylesheet" href="./index.css" />
    </head>

    <body>
        <header>
            <h1>WebCam Tuner</h1>
        </header>

        <main>


            <!-- Fragment shader program for depth -->
            <!-- Only difference is in multiplication -->
            <script id="depth-fs" type="x-shader/x-fragment">
                precision mediump float;
                uniform sampler2D s;
                varying vec2 t;

                void main(void)
                {
                    vec4 tex=texture2D(s,t) * vec4(10.0, 10.0, 10.0, 1.0);
                    gl_FragColor=tex.rgba;
                }
            </script>





    <!-- Vertex shader program -->
    <script id="shader-vs" type="x-shader/x-vertex">
        #version 300 es
        in vec4 a_position;
        void main() {
            gl_Position = a_position - 0.5;
        }
    </script>

    <!-- Fragment shader program -->
    <script id="packing-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D u_texture;

        layout(location = 0) out vec4 outColor;
        layout(location = 1) out vec4 fragCoord;

        int iwidth = 8; // width of the pixel block, packed into 8 bits
        int iheight = 4; // height of the pixel block, packed into RGBA channels
        vec4 bytes = vec4(0.0, 0.0, 0.0, 0.0);

        void main() {

            // gl_FragCoord gives exact pixel coordinates in window space, not interpolated
            // in the middle of the pixel (0.5, 0.5) for pixel (0, 0)
            int x = int(gl_FragCoord.x - 0.5) * 8;
            int y = int(gl_FragCoord.y - 0.5) * 4;
            ivec2 xy = ivec2(x, y);
            for (int j = 0; j < iheight; j++) {
                uint byteValue = 0u;
                for(int i = 0; i < iwidth; i++) {
                    float normCol = texelFetch(u_texture, xy + ivec2(i,j), 0).r;
                    if (normCol > 0.24) {
                        byteValue =  byteValue | (1u << uint(i));
                    }
                }
                bytes[j] = float(byteValue) / 255.0; // normalize to [0, 1] range
            }
            outColor = bytes;
            fragCoord = gl_FragCoord;
        }
    </script>

        <!-- Fragment shader program -->
    <script id="dilation-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D p_texture;
        out vec4 outColor;

        int iwidth = 8; // width of the pixel block, packed into 8 bits
        int iheight = 4; // height of the pixel block, packed into RGBA channels

        vec4 bytes = vec4(0.0, 0.0, 0.0, 0.0);


        uint[12] getDataBock(sampler2D tex) {
            // In GLSL, arrays are automatically initialized to zero if no initializer is provided,
            // though this behavior can vary by GLSL version and implementation.
            // For most reliable results, explicitly initialize each element.
            uint[12] data = uint[12](0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u);
            ivec2 xy = ivec2(gl_FragCoord.xy);

            for (int j = -1; j < 2; j++) {
                for(int i = -1; i < 2; i++) {
                    vec4 bytes = texelFetch(tex, xy + ivec2(i, j), 0) * 255.0; // .rgba
                    for(int cj = 0; cj < 4; cj++) {
                        data[4*j + 4 + cj] = data[4*j + 4 + cj] | (uint(bytes[cj]) << uint(8 * i + 8));
                    }
                }
            }
            return data;
        }

        uint[14] getTransposedBock(uint[12] data) {
            uint[14] cols = uint[14](0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u, 0u);
            for (int row = 0; row < 12; row++) {
                // Extract the middle 12 bits (bits 6-17) from the 24-bit row
                uint middleBits = (data[row] >> 5u) & 0x3FFFu;
                for (int col = 0; col < 14; col++) {
                    uint bitValue = (middleBits >> uint(13 - col)) & 1u;
                    cols[13-col] = (cols[13-col] & ~(1u << uint(row))) | (bitValue << uint(row));
                }
            }
            return cols;
        }

        void main() {

            uint data[12] = getDataBock(p_texture);
            uint cols[14] = getTransposedBock(data);

            for (int j = 0; j < iheight; j++) {
                uint byteValue = 0u;
                for(int i = 0; i < iwidth; i++) {
                    uint crossRow = (data[j + 4] >> uint(i+5)) & 0x7Fu;
                    uint crossCol = (cols[i + 3] >> uint(j+1)) & 0x7Fu;
                    // if( bool(data[j + 4] & (1u << uint(i+8))) )  {
                    if( bool(crossRow + crossCol) ) {
                        // If any of the bits in the cross is set, set the bit in the byte
                        byteValue =  byteValue | (1u << uint(i));
                    }
                }
                bytes[j] = float(byteValue) / 255.0;
            }
            outColor = bytes;
        }
    </script>



    <!-- Fragment shader program -->
    <script id="unpacking-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D p_texture;
        out vec4 outColor;

        int iwidth = 8; // width of the pixel block, packed into 8 bits
        int iheight = 4; // height of the pixel block, packed into RGBA channels

        void main() {

            ivec2 xyOrig = ivec2(gl_FragCoord.xy - 0.5);
            int dx = xyOrig.x % iwidth;
            int dy = xyOrig.y % iheight; // to decide index in .rgba array
            int x = (xyOrig.x - dx) / iwidth;
            int y = (xyOrig.y - dy) / iheight;

            uint byte = uint( texelFetch(p_texture, ivec2(x,y), 0)[dy] * 255.0 );
            uint bit = byte & (1u << uint(dx));
            float val = float(bool(bit));
            outColor = vec4(val, val, val, 1.0);
        }
    </script>

    <!-- Fragment shader program -->
    <script id="comparison-fs" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;

        uniform sampler2D src_texture;
        uniform sampler2D p_texture;
        uniform float u_threshold;

        out vec4 outColor;

        void main() {
            ivec2 texCoord = ivec2(gl_FragCoord.xy - 0.5);
            vec4 color1 = texelFetch(src_texture, texCoord, 0);
            vec4 color2 = texelFetch(p_texture, texCoord, 0);

            if(all(lessThan(abs(color1 - color2), vec4(u_threshold)))) {
                discard;
            }

            // Otherwise output difference, or something, doesn't matter
            outColor = abs(color1 - color2);
        }
    </script>

    <!-- Fragment shader program -->
    <script id="copy-fs" type="x-shader/x-fragment">
        #version 300 es
        // precision mediump float;
        precision highp float;
        uniform sampler2D up_texture;
        out vec4 outColor;

        void main() {
            ivec2 texCoord = ivec2(gl_FragCoord.xy - 0.5);
            outColor = texelFetch(up_texture, texCoord, 0);
        }
    </script>




            <script type="module">
                const footer = document.querySelector("footer");
                document.body.appendChild(
                    document.createElement("screen-logger"),
                );
                document.body.insertBefore(
                    document.createElement("media-menu"),
                    footer
                );
            </script>

            <script type="module" src="./utils/Logger.js"></script>
            <script type="module" src="./mediaMenu.js"></script>
        </main>

        <footer>
            <p>Kind of paradox</p>
        </footer>
    </body>
</html>
